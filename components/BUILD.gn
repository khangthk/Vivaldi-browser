
source_set("adverse_adblocking") {
  sources = [
    "adverse_adblocking/adverse_ad_filter_list.cc",
    "adverse_adblocking/adverse_ad_filter_list.h",
    "adverse_adblocking/vivaldi_subresource_filter_throttle.cc",
    "adverse_adblocking/vivaldi_subresource_filter_throttle.h",
    "adverse_adblocking/vivaldi_subresource_filter_throttle_manager.cc",
    "adverse_adblocking/vivaldi_subresource_filter_throttle_manager.h",
    "adverse_adblocking/adverse_ad_filter_list_factory.h",
  ]
  deps = [
    "//build:branding_buildflags",
    "//components/prefs",
    "//components/keyed_service/content",
    "//components/safe_browsing/core/db:v4_protocol_manager_util",
    "//components/subresource_filter/core/browser",
    "//components/subresource_filter/core/common",
    "//components/subresource_filter/content/browser",
    "//content/public/browser",
    "//extensions/buildflags",
    "//vivaldi/prefs:prefs_files_generated",
  ]
}

action("vivaldi_prepopulated_search_engines") {
  script = "search_engines/merge_searchengines.py"

  chromium_json = "//components/search_engines/prepopulated_engines.json"
  vivaldi_json = "//vivaldi/components/search_engines/vivaldi_prepopulated_engines.json"

  output_json = "$root_gen_dir/components/search_engines/prepopulated_engines.json"

  inputs = [
    chromium_json,
    vivaldi_json
  ]

  outputs = [
    output_json,
  ]

  args = [
    rebase_path(chromium_json),
    rebase_path(vivaldi_json),
    rebase_path(output_json),
  ]
}
