# Copyright (c) 2019 Vivaldi Technologies AS. All rights reserved

import("//testing/test.gni")

source_set("adblock_parser") {
  sources = [
    "adblock_content_injection_rule.cc",
    "adblock_content_injection_rule.h",
    "adblock_request_filter_rule.cc",
    "adblock_request_filter_rule.h",
    "adblock_metadata.cc",
    "adblock_metadata.h",
    "adblock_rule_parser.cc",
    "adblock_rule_parser.h",
    "adblock_ruleset_file_parser.cc",
    "adblock_ruleset_file_parser.h",
    "ddg_rules_parser.cc",
    "ddg_rules_parser.h",
    "parse_result.cc",
    "parse_result.h",
    "parse_utils.cc",
    "parse_utils.h",
  ]
  deps = [
    "//base",
    "//base:i18n",
    "//url",
  ]
}

source_set("adblock_filter") {
  sources = [
    "adblock_content_injection_provider.cc",
    "adblock_content_injection_provider.h",
    "adblock_cosmetic_filter.cc",
    "adblock_cosmetic_filter.h",
    "adblock_known_sources_handler_impl.cc",
    "adblock_known_sources_handler_impl.h",
    "adblock_known_sources_handler.cc",
    "adblock_known_sources_handler.h",
    "adblock_request_filter.cc",
    "adblock_request_filter.h",
    "adblock_resources.h",
    "adblock_resources.cc",
    "adblock_rule_pattern_matcher.cc",
    "adblock_rule_pattern_matcher.h",
    "adblock_rule_service_factory.cc",
    "adblock_rule_service_factory.h",
    "adblock_rule_service_impl.cc",
    "adblock_rule_service_impl.h",
    "adblock_rule_service.cc",
    "adblock_rule_service.h",
    "adblock_rule_service_storage.cc",
    "adblock_rule_service_storage.h",
    "adblock_rule_source_handler.cc",
    "adblock_rule_source_handler.h",
    "adblock_rules_index_builder.cc",
    "adblock_rules_index_builder.h",
    "adblock_rules_index_manager.cc",
    "adblock_rules_index_manager.h",
    "adblock_rules_index.cc",
    "adblock_rules_index.h",
    "blocked_urls_reporter_tab_helper.cc",
    "blocked_urls_reporter_tab_helper.h",
    "blocked_urls_reporter.cc",
    "blocked_urls_reporter.h",
    "stylesheet_builder.cc",
    "stylesheet_builder.h",
    "utils.cc",
    "utils.h",
  ]

  deps = [
    ":adblock_parser",
    ":package_adblocker_files",
    "mojom",
    "//base:base",
    "//base:i18n",
    "//base/util/values:values_util",
    "//components/keyed_service/content",
    "//components/url_pattern_index",
    "//content/public/browser",
    "//url",
    "//third_party/re2",
    "//vivaldi/components/request_filter/adblock_filter/flat:adblock_rules_list",
    "//third_party/blink/public/common",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "adblock_rule_parser_unittest.cc",
    "ddg_rules_parser_unittest.cc",
  ]

  deps = [
    ":adblock_parser",
     "//base",
    "//testing/gtest",
  ]
}

ublock_dir = "//vivaldi/thirdparty/ublock"
ublock_resources = ublock_dir + "/src/web_accessible_resources"
adblockplus_snippets = "//vivaldi/thirdparty/adblockpluscore/lib/content/snippets.js"
adblockplus_snippets_loader = "//vivaldi/thirdparty/adblockpluscore/lib/snippets.js"
compiled_adblockplus_snippets_loader_dir = "$root_out_dir/gen/components/request_filter/adblock_filter/abp_snippets"
compiled_adblockplus_snippets_loader = compiled_adblockplus_snippets_loader_dir + "/snippets.js"
resources_output = "$root_out_dir/resources/vivaldi/adblocker_resources"

copy("ublock_license") {
  sources = [
    "$ublock_dir/LICENSE.txt",
  ]
  outputs = [
    "$resources_output/{{source_file_part}}",
  ]
}

action("make_snippets_loader") {
  script = "//vivaldi/components/request_filter/adblock_filter/abp_snippets_tools/make_snippets_loader.py"

  sources = [
    adblockplus_snippets,
    adblockplus_snippets_loader
  ]

  outputs = [
    compiled_adblockplus_snippets_loader_dir,
    compiled_adblockplus_snippets_loader
  ]

  args = [
    "--loader", rebase_path(adblockplus_snippets_loader, root_build_dir),
    "--snippets", rebase_path(adblockplus_snippets, root_build_dir),
    "--output", rebase_path(compiled_adblockplus_snippets_loader, root_build_dir),
  ]
}

action("package_redirectable_resources") {
  resources = "$resources_output/redirectable_resources.json"

  sources = [
    ublock_resources,
  ]

  outputs = [
    resources
  ]

  script = "//vivaldi/tools/resources2json/resources2json.py"

  args = [
    "--input_dir", rebase_path(ublock_resources, root_build_dir),
    "--output", rebase_path(resources, root_build_dir),
    "--exclude", "README.txt", "empty",
    "--add-empty", "noop.css"
  ]
}

action("package_injectable_resources") {
  resources = "$resources_output/injectable_resources.json"

  deps= [
    ":make_snippets_loader",
  ]

  sources = [
    compiled_adblockplus_snippets_loader_dir,
  ]

  outputs = [
    resources
  ]

  script = "//vivaldi/tools/resources2json/resources2json.py"

  args = [
    "--input_dir", rebase_path(compiled_adblockplus_snippets_loader_dir, root_build_dir),
    "--output", rebase_path(resources, root_build_dir),
  ]
}

group("package_adblocker_files") {
  deps = [
    # adblockplus and ublock use the same license, so we only need one of their
    # license files
    ":ublock_license",
    ":package_redirectable_resources",
    ":package_injectable_resources"
  ]
}