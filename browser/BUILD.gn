import("//vivaldi/gn/config/version.gni")
import("//build/config/jumbo.gni")
import("//build/util/version.gni")
import("//extensions/buildflags/buildflags.gni")

static_library("browser") {
  public_deps = [
    "//vivaldi/app",
    "//vivaldi/components:content_adverse_adblocking",
    "//vivaldi/clientparts",
    "//vivaldi/importer:browser",
    "//vivaldi/prefs:prefs_files_generator",
    "//vivaldi/ui",
  ]
  if (!is_android) {
    public_deps += [
      "//vivaldi/components/datasource",
      "//vivaldi/extensions",
      "//vivaldi/extensions/api:apis",
      "//vivaldi/extensions/schema",
      "//vivaldi/vivapp:vivaldi_app",
    ]
  }
  deps = [
    ":vivaldi_version_info",
    "//vivaldi/prefs:browser_prefs",

    #"//vivaldi/base",
    "//base:base",
    "//chrome/app:chromium_strings",
    "//chrome/app:command_ids",
    "//chrome/app:generated_resources",
    "//chrome/app:google_chrome_strings",
    "//chrome/browser:resources",
    "//chrome/browser:theme_properties",
    "//chrome/browser/devtools",
    "//chrome/common:non_code_constants",

    "//components/bookmarks/browser",
    "//components/keyed_service/content",
    "//components/metrics:metrics",
    "//components/os_crypt:os_crypt",
    "//components/renderer_context_menu",
    "//components/search_engines",
    "//components/strings:components_chromium_strings",
    "//components/strings:components_google_chrome_strings",
    "//components/strings:components_strings",
    "//components/version_info:generate_version_info",
   # "//content/browser",
    "//crypto:crypto",
    "//skia:skia",
    "//third_party/blink/public:blink_headers",
    "//ui/events:events_base",
    "//vivaldi/components:adverse_adblocking",
    "//vivaldi/prefs:prefs_strings",
  ]
  if (is_win) {
    deps += [ "//vivaldi/third_party:sparkle" ]
  } else if (is_mac) {
    deps += [
      #"//vivaldi/third_party:copy_sparkle_framework",
    ]
  }
  if (!is_android) {
    deps += [
      "//components/guest_view/browser",
      "//ui/views",
    ]
  }
  if (enable_extensions) {
    deps += [
      "//chrome/browser/web_applications:web_applications_on_extensions",
      "//chrome/browser/web_applications/extensions",
      "//chrome/browser/extensions",
    ]
  }
  allow_circular_includes_from = [
    "//components/renderer_context_menu",
    "//vivaldi/app",
  ]
  if (enable_extensions) {
    allow_circular_includes_from += [
      "//chrome/browser/extensions",
      "//vivaldi/extensions/api:apis",
    ]
  }
  sources = [
    "stats_reporter_impl_android.cc",
    "stats_reporter_impl_linux.cc",
    "stats_reporter_impl_mac.mm",
    "stats_reporter_impl_win.cc",
    "stats_reporter_impl.cc",
    "stats_reporter_impl.h",
    "stats_reporter.cc",
    "stats_reporter.h",
    "../components/adverse_adblocking/adverse_ad_filter_list_factory.cc",
    "../components/adverse_adblocking/adverse_ad_filter_list_factory.h",
    "thumbnails/capture_page.cc",
    "thumbnails/capture_page.h",
    "vivaldi_default_bookmarks.cc",
    "vivaldi_default_bookmarks.h",
  ]
  if (!is_android) {
    sources += [
      "../app/vivaldi_command_controller.cpp",
      "../app/vivaldi_command_controller.h",
      "../app/vivaldi_commands.h",
      "menus/vivaldi_bookmark_context_menu.cpp",
      "menus/vivaldi_bookmark_context_menu.h",
      "menus/vivaldi_context_menu_controller.h",
      "menus/vivaldi_context_menu_controller.cc",
      "menus/vivaldi_developertools_menu_controller.h",
      "menus/vivaldi_developertools_menu_controller.cc",
      "menus/vivaldi_menus.cpp",
      "menus/vivaldi_menus.h",
      "menus/vivaldi_menubar.cc",
      "menus/vivaldi_menubar.h",
      "menus/vivaldi_pwa_menu_controller.h",
      "menus/vivaldi_pwa_menu_controller.cc",
      "vivaldi_browser_finder.cc",
      "vivaldi_browser_finder.h",
      "vivaldi_webcontents_util.cc",
      "vivaldi_webcontents_util.h",
      "thumbnails/thumbnail_capture_contents.cc",
      "thumbnails/thumbnail_capture_contents.h",
    ]
  }
  if (is_mac) {
    sources += [
      "vivaldi_app_observer.h",
      "vivaldi_app_observer.mm",
    ]
  } else {
    #deps += [ "//chrome/utility" ]
  }
}

static_library("clipboard") {
  sources = [
    "vivaldi_clipboard_utils.cc",
    "vivaldi_clipboard_utils.h",
  ]

  deps= [ "//third_party/blink/public:blink_headers" ]
}

action("vivaldi_version_info") {
  script = vivaldi_version_py_path
  inputs = [
    vivaldi_version_path,
  ]
  outputs = [
    "$root_build_dir/vivaldi_version.txt",
  ]
  args = [
           "-f",
           rebase_path(vivaldi_version_path, root_build_dir),
           "-f",
           rebase_path(chrome_version_path, root_build_dir),
           "-f",
           rebase_path(lastchange_path, root_build_dir),
           "-e",
           "VIVALDI_BUILD=$GLOBAL_BUILDNUM",
           "--vivaldi",
           "-o",
         ] + rebase_path(outputs, root_build_dir)
}

source_set("init_sparkle") {
  sources = [
    "init_sparkle.cc",
    "init_sparkle.h",
  ]

  deps = [
    "//base",
    "//base:i18n",
    "//base:base_static",
    "//url",
  ]

  if (is_win) {
    deps += [ "//vivaldi/third_party:sparkle" ]
  }
}
