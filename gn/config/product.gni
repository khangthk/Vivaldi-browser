declare_args() {
  build_branch = "master"
  force_sopranos_build = false
  is_oem_automotive_build = is_vivaldi_polestar
  is_final_build = false
}

_vivaldi_version_py_path = "//build/util/version.py"

_vivaldi_product_kind_file = "//vivaldi/VIVALDI_PRODUCT"

_vivaldi_release_py_path = "//vivaldi/scripts/get_preview.py"

release_kind =
    exec_script(_vivaldi_release_py_path, [ build_branch ], "trim string")
official_product_kind_string =
    exec_script(_vivaldi_version_py_path,
                [
                  "-f",
                  rebase_path(_vivaldi_product_kind_file, root_build_dir),
                  "-t",
                  "@VIVALDI_PRODUCT@",
                ],
                "trim string",
                [_vivaldi_product_kind_file])

android_vivaldi_manifest_package = "com.vivaldi.browser.sopranos"
if (release_kind == "final") {
  is_final_build = true
  extra_grit_defines = [
    "-D",
    "vivaldi_release_final",
  ]
  VIVALDI_RELEASE_KIND = "vivaldi_final"
  if (is_oem_automotive_build) {
    # Vivaldi/Polestar
    android_vivaldi_manifest_package = "com.polestar.vivaldi.production.android"
  } else {
    android_vivaldi_manifest_package = "com.vivaldi.browser"
  }
} else if (is_official_build && !force_sopranos_build) {
  extra_grit_defines = [
    "-D",
    "vivaldi_release_snapshot",
  ]
  VIVALDI_RELEASE_KIND = "vivaldi_snapshot"
  android_vivaldi_manifest_package = "com.vivaldi.browser.snapshot"
} else {
  extra_grit_defines = [
    "-D",
    "vivaldi_release_sopranos",
  ]
  VIVALDI_RELEASE_KIND = "vivaldi_sopranos"
}
